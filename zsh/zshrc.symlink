# shortcut to this dotfiles path is $DOT
export DOT=$HOME/.dotfiles

export ZSH=$HOME/.oh-my-zsh
ZSH_THEME="philips"
plugins=(git)

source $ZSH/oh-my-zsh.sh

# all of our zsh files
typeset -U config_files
setopt extendedglob
config_files=($DOT/(^oh-my-zsh.symlink/)#^oh-my-zsh.symlink/*.zsh)
unsetopt extendedglob

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

source ~/.secrets.zsh
