# shortcut to this dotfiles path is $DOT
export DOT=$HOME/.dotfiles
ZSH_THEME="philips"
plugins=(git)

# all of our zsh files
typeset -U config_files
config_files=($DOT/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

source ~/.secrets.zsh

unsetopt RM_STAR_SILENT
setopt RM_STAR_WAIT
unset RPROMPT
